load("@bazel_skylib//rules:write_file.bzl", "write_file")

# This library is for internal hiredis use, because hiredis assumes a
# different include prefix for itself than external libraries do.
cc_library(
    name = "_hiredis",
    hdrs = [
        "deps/hiredis/dict.c",
    ],
    strip_include_prefix = "deps/hiredis",
)

cc_library(
    name = "hiredis",
    srcs = glob(
        [
            "deps/hiredis/*.c",
            "deps/hiredis/*.h",
        ],
        exclude =
        [
            "deps/hiredis/test.c",
        ],
    ),
    hdrs = glob([
        "deps/hiredis/*.h",
        "deps/hiredis/adapters/*.h",
    ]),
    includes = ["deps/hiredis"],
    strip_include_prefix = "deps",
    deps = [
        ":_hiredis",
    ],
    visibility = ["//visibility:public"],
)

write_file(
    # Bazelified version of: src/mkreleasehdr.sh
    name = "mkreleasehdr",
    out = "src/release.h",
    content = [
        "#define REDIS_GIT_SHA1 \"00000000\"",
        "#define REDIS_GIT_DIRTY \"0\"",
        "#define REDIS_BUILD_ID \"0\"",
    ],
)

cc_library(
    name = "jemalloc",
    srcs = glob(["deps/jemalloc/src/*.c"]),
    hdrs = glob(["deps/jemalloc/include/**/*.h"]),
    strip_include_prefix = "deps/jemalloc/include",
    visibility = ["//visibility:public"],
)

cc_library(
    name = "linenoise",
    srcs = [
        "deps/linenoise/linenoise.c",
        "deps/linenoise/linenoise.h",
    ],
    hdrs = ["deps/linenoise/linenoise.h"],
    strip_include_prefix = "deps/linenoise",
    visibility = ["//visibility:public"],
)

cc_library(
    name = "lua",
    srcs = glob([
        "deps/lua/src/*.c",
        "deps/lua/src/*.h",
    ]) + [
        "src/solarisfixes.h",
    ],
    hdrs = glob([
        "deps/lua/src/*.h",
    ]),
    local_defines = [
        "ENABLE_CJSON_GLOBAL",
        "LUA_ANSI",
    ],
    strip_include_prefix = "deps/lua/src",
    visibility = ["//visibility:public"],
)

cc_library(
    name = "libredis",
    srcs = [],
    hdrs = glob([
        "src/*.h",
        "src/ae_*.c",
    ]),
    strip_include_prefix = "src",
    visibility = ["//visibility:public"],
)

cc_binary(
    name = "redis-cli",
    srcs = glob(
        [
            # These must end up equal to REDIS_CLI_OBJ in redis/src/Makefile
            "src/adlist.c",
            "src/ae.c",
            "src/anet.c",
            "src/crc*.c",
            "src/dict.c",
            "src/redis-cli.c",
            "src/release.c",
            "src/siphash.c",
            "src/zmalloc.c",
        ],
    ) + [
        # Other items not listed directly in the Makefile
        ":mkreleasehdr",
    ],
    includes = ["src"],
    visibility = ["//visibility:public"],
    deps = [
        # ":jemalloc",
        ":hiredis",
        ":libredis",
        ":linenoise",
        ":lua",
    ],
)

cc_binary(
    name = "redis-server",
    srcs = glob(
        # These globs must end up equal to REDIS_SERVER_OBJ in redis/src/Makefile
        [
            "src/*.c",
        ],
        exclude =
        [
            "src/ae_*.c",
            "src/redis-benchmark.c",
            "src/redis-cli.c",
        ],
    ) + [
        # Other items not listed directly in the Makefile
        ":mkreleasehdr",
    ],
    includes = ["src"],
    local_defines = [
        "REDIS_STATIC=",
        # "USE_JEMALLOC",
    ],
    linkopts = select({
        "@bazel_tools//src/conditions:windows": [
        ],
        "@bazel_tools//src/conditions:darwin": [
        ],
        "//conditions:default": [
            "-lm",
            "-ldl",
            "-lpthread",
            "-lrt",
        ],
    }),
    visibility = ["//visibility:public"],
    deps = [
        # ":jemalloc",
        ":hiredis",
        ":libredis",
        ":lua",
    ],
)
